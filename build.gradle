buildscript {
    ext.kotlin_version = "1.2.0-rc-39"

    repositories {
        jcenter()
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2' }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.jaci.openrio.gradle:GradleRIO:2017.2.3"
        classpath "net.saliman:gradle-cobertura-plugin:2.4.0"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1"
    }
}

apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "jaci.openrio.gradle.GradleRIO"
apply plugin: "net.saliman.cobertura"
apply plugin: "com.github.kt3k.coveralls"
deploy.dependsOn "assemble"
check.dependsOn "ktlint"

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2' }
    maven { url 'http://first.wpi.edu/FRC/roborio/maven/development' }
    maven { url 'http://dev.imjac.in/maven' }
}

configurations {
    ktlint
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile "com.github.SUPERCILEX:strongback-java:f05322e2dd"

    compile "edu.wpi.first.wpilibj:wpilibj-java:2017.3.1-20170819010033-68-ge1195e8"
    compile "edu.wpi.first.wpilibj:athena:2017.3.1"
    compile "edu.wpi.first.wpilibj:wpilibj-jniShared:2017.3.1-20171011234223-117-gf0cc623"
    compile "edu.wpi.first.wpilibj:athena-jni:2017.3.1"
    compile "edu.wpi.first.wpilibj:wpilibj-jni:2017.3.1-20171011234223-117-gf0cc623"
    compile "edu.wpi.first.wpilib:athena-runtime:2017.3.1"
    compile "edu.wpi.first.wpilib:SmartDashboard:2.0.3-20170227114112-1-ge6e275d"
    compile "edu.wpi.first.wpilib.networktables.java:NetworkTables:3.1.7"
    compile "edu.wpi.first.cscore:cscore-java:1.0.2-20171001140620-15-g223e61d"
    compile "org.opencv:opencv-java:3.2.0"
    compile "thirdparty.frc.ctre:Toolsuite-Java:5.0.2.1"

    ktlint 'com.github.shyiko:ktlint:0.9.0'

    testCompile "org.easytesting:fest-assert:1.4"
    testCompile "junit:junit:4.12"
    testRuntime "org.slf4j:slf4j-api:1.7.10"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

frc {
    team = "2521"
    robotClass = "org.sert2521.gamename.Robot"
    rioIP = "10.25.21.89"
}

def robotManifest = {
    attributes "Main-Class": "edu.wpi.first.wpilibj.RobotBase"
    attributes "Robot-Class": frc.robotClass
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest robotManifest
}

test {
    testLogging {
        showStandardStreams = true
    }
}

cobertura {
    coverageFormats = ["html", "xml"]
    coverageExcludes = [".*org.sert2521.gamename.Robot", ".*org.sert2521.gamename.OI"]
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
